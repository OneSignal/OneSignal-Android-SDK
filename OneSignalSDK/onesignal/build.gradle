plugins {
    id 'com.android.library'
    // There isn't Kotlin or Java code in this top-level project,
    // however consumers look for an .aar file, which this causes it
    // to be created.
    id 'kotlin-android'
    id 'com.vanniktech.maven.publish'
}

android {
    compileSdkVersion rootProject.buildVersions.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.buildVersions.minSdkVersion
    }

    buildTypes {
        original {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
        unity {
            minifyEnabled false
        }
    }
    // Forced downgrade to Java 8 so SDK is backwards compatible in consuming projects
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.onesignal'

    kotlinOptions.freeCompilerArgs += ['-module-name', namespace]
}

ext {
    projectName = "OneSignal SDK"
    projectDescription = "OneSignal Android SDK - All Modules"
}

dependencies {
    api project(':OneSignal:core')
    api project(':OneSignal:notifications')
    api project(':OneSignal:in-app-messages')
    api project(':OneSignal:location')
}

apply from: 'maven-push.gradle'
