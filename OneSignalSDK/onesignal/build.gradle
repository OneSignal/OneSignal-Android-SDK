apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    defaultConfig {
        // Normally we shouldn't need to redefine manifestPlaceholders for our onesignal project
        //   but Android Studio gets a sync error if these are not here.
        //      - /gradlew onesignal:assembleRelease is fine from the console.
        // We can't use static values are these are directly added to the AndroidManifest.xml in the .aar file.
        manifestPlaceholders = [onesignal_app_id: '${onesignal_app_id}',
                                // Project number pulled from dashboard, local value is ignored
                                onesignal_google_project_number: '${onesignal_google_project_number}']
        minSdkVersion 15
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    buildTypes {
        original {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
        }
        unity {
            minifyEnabled false
        }
    }
}

// api || implementation = compile and runtime

// KEEP: version ranges, these get used in the released POM file for maven central
dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    // play-services-gcm:16.1.0 is the last version before going to AndroidX
    // compileOnly as this is just for fallback code if FCM wasn't added to the project.
    // Remove for 4.0.0 release
    compileOnly 'com.google.android.gms:play-services-gcm:16.1.0'

    // play-services-location:16.0.0 is the last version before going to AndroidX
    // Can be compileOnly in 4.0.0, can't change until then as some projects may rely on this
    implementation 'com.google.android.gms:play-services-location:[10.2.1, 16.0.99]'

    // play-services-ads-identifier:15.0.0 first version
    //   Classes used to live in play-services-base before this
    // play-services-ads-identifier:16.0.0 is the last version before going to AndroidX
    implementation 'com.google.android.gms:play-services-ads-identifier:[15.0.0, 16.0.99]'

    // :play-services-base:16.1.0 is the last version before going to AndroidX
    // Required for GoogleApiAvailability
    implementation 'com.google.android.gms:play-services-base:[10.2.1, 16.1.99]'

    // firebase-messaging:18.0.0 is the last version before going to AndroidX
    // firebase-messaging:17.6.0 is the max version since we still have code looking for FirebaseInstanceIdService
    api 'com.google.firebase:firebase-messaging:[10.2.1, 17.3.99]'

    // Keep under 28 until we switch to AndroidX
    //   otherwise app can get dup classes between 26 & 28 when mixing these versions.
    // Also note, firebase & gms libraries use android.support:26.
    //   - They never refer to 27 or 28
    api 'com.android.support:cardview-v7:[26.0.0, 27.99.99]'
    api 'com.android.support:support-fragment:[26.0.0, 27.99.99]'
    api 'com.android.support:customtabs:[26.0.0, 27.99.99]'

    // compileOnly as this is just for fallback code if AppCompatActivity wasn't added to the project.
    compileOnly 'com.android.support:appcompat-v7:26.1.0'
}

apply from: 'maven-push.gradle'
