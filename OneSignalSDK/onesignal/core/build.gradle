plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.diffplug.spotless'
    id 'com.vanniktech.maven.publish'
    id 'io.gitlab.arturbosch.detekt'
}

android {
    compileSdkVersion rootProject.buildVersions.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.buildVersions.minSdkVersion
        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "SDK_VERSION", "\"${rootProject.version}\""
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        original {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
        }
        unity {
            minifyEnabled false
        }
    }
    testOptions {
        unitTests.all {
            maxParallelForks 1
            maxHeapSize '3072m'
            jvmArgs '-XX:MaxMetaspaceSize=256m', '-XX:+UseG1GC', '-XX:+UseStringDeduplication'
        }
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            it.useJUnitPlatform()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.onesignal.core'

    kotlinOptions.freeCompilerArgs += ['-module-name', namespace]
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams false // Enable to have logging print
    }
}

ext {
    projectName = "OneSignal SDK Core"
    projectDescription = "OneSignal Android SDK - Core Module"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // AndroidX Lifecycle and Activity
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.activity:activity-ktx:$activityVersion"

    compileOnly('com.amazon.device:amazon-appstore-sdk:[3.0.1, 3.0.99]')

    api('androidx.appcompat:appcompat') {
        version {
            require '[1.0.0, 1.3.99]'
            prefer '1.3.1'
        }
    }

    implementation('io.opentelemetry:opentelemetry-api:1.55.0')
    implementation('io.opentelemetry:opentelemetry-sdk:1.55.0')
    // Docs sounds like okhttp is already included...
    implementation('io.opentelemetry:opentelemetry-exporter-sender-okhttp:1.55.0')
    implementation('io.opentelemetry:opentelemetry-exporter-otlp:1.55.0')
    implementation('io.opentelemetry.semconv:opentelemetry-semconv:1.37.0')
    // TODO: Make sure by adding Android OTel that it doesn't auto send stuff to us



    testImplementation(project(':OneSignal:testhelpers'))

    testImplementation("io.kotest:kotest-runner-junit5:$kotestVersion")
    testImplementation("io.kotest:kotest-runner-junit5-jvm:$kotestVersion")
    testImplementation("io.kotest:kotest-assertions-core:$kotestVersion")
    testImplementation("io.kotest:kotest-property:$kotestVersion")
    testImplementation("org.robolectric:robolectric:4.10.3")
    // kotest-extensions-android allows Robolectric to work with Kotest via @RobolectricTest
    testImplementation("br.com.colman:kotest-extensions-android:0.1.1")
    testImplementation("androidx.test:core-ktx:1.4.0")
    testImplementation("androidx.test:core:1.4.0")
    testImplementation("io.mockk:mockk:$ioMockVersion")
    testImplementation("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion")

    // com.tdunning:json is needed for non-Robolectric tests.
    testImplementation("com.tdunning:json:$tdunningJsonForTest")
}

apply from: '../detekt.gradle'
apply from: '../spotless.gradle'
apply from: '../maven-push.gradle'
