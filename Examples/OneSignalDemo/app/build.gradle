plugins {
    id 'com.android.application'
}

android {
    namespace = "com.onesignal.sdktest"

    buildFeatures {
        buildConfig = true
    }

    compileSdkVersion 35
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "default"
    }

//    signingConfigs {
//        huawei {
//            storeFile file('SdkTest.jks')
//            keyAlias 'SdkTest'
//            keyPassword ''
//            storePassword ''
//            v1SigningEnabled true
//            v2SigningEnabled true
//        }
//    }

    productFlavors {
        gms {
            dimension "default"
            applicationId "com.onesignal.sdktest"
        }
        huawei {
            dimension "default"
            minSdkVersion 21
            applicationId "com.onesignal.sdktest"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
//            signingConfig null
//            productFlavors.huawei.signingConfig signingConfigs.huawei
            debuggable true
        }
    }

    packagingOptions {
        resources {
            excludes += ['androidsupportmultidexversion.txt']
        }
    }


    task flavorSelection() {
        if (getGradle().getStartParameter().getTaskRequests().toString().contains("Gms")) {
            apply plugin: 'com.google.gms.google-services'
            googleServices { disableVersionCheck = true }
        } else {
            apply plugin: 'com.huawei.agconnect'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    implementation 'com.github.bumptech.glide:glide:4.12.0'

    /** START - Google Play Builds **/
    gmsImplementation('com.onesignal:OneSignal:5.1.30')
    /** END - Google Play Builds **/

    /** START - Huawei Builds **/
    // Omit Google / Firebase libraries for Huawei builds.
    huaweiImplementation('com.onesignal:OneSignal:5.1.30') {
        exclude group: 'com.google.android.gms', module: 'play-services-gcm'
        exclude group: 'com.google.android.gms', module: 'play-services-analytics'
        exclude group: 'com.google.android.gms', module: 'play-services-location'
        exclude group: 'com.google.firebase', module: 'firebase-messaging'
    }
    // Add HMS Push library
    huaweiImplementation 'com.huawei.hms:push:6.3.0.304'
    // Optionally add HMS location
    huaweiImplementation 'com.huawei.hms:location:4.0.0.300'
    /** END - Huawei Builds **/
}
